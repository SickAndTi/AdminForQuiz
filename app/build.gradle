apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'
apply plugin: "io.fabric"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.scp.adminforquiz"
        minSdkVersion 19
        targetSdkVersion 28
        multiDexEnabled true
        versionCode 15
        versionName "1.5"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [toothpick_registry_package_name: 'com.scp.adminforquiz']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfigs {
                release {
                    // Load keystore
                    if (rootProject.file('keystore.properties').exists()) {
                        def keystorePropertiesFile = rootProject.file("keystore.properties")
                        def keystoreProperties = new Properties()
                        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                        storeFile file(keystoreProperties['storeFile'])
                        storePassword keystoreProperties['storePassword']
                        keyAlias keystoreProperties['keyAlias']
                        keyPassword keystoreProperties['keyPassword']
                    }
                }
            }
            if (rootProject.file('keystore.properties').exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    flavorDimensions "mode"

    productFlavors {
        prod {
            manifestPlaceholders = [myDebuggable: false]
            buildConfigField "boolean", "TIMBER_ENABLE", "false"
            dimension "mode"
        }
        dev {
            dimension "mode"
            manifestPlaceholders = [myDebuggable: true]
            aaptOptions.cruncherEnabled = false
            resConfigs("ru", "xxhdpi")
        }

        applicationVariants.all { variant ->
            println("variantCore: " + variant.productFlavors[0].name)

            if (rootProject.file("config/" + variant.productFlavors[0].name + "/config.properties").exists()) {
                def properties = new Properties()
                properties.load(new FileInputStream(rootProject.file("config/" + variant.productFlavors[0].name + "/config.properties")))

                buildConfigField('String', 'API_URL', '"' + properties.get("apiUrl") + '"')
                buildConfigField('String', 'CLIENT_ID', '"' + properties.get("clientId") + '"')
                buildConfigField('String', 'CLIENT_SECRET', '"' + properties.get("clientSecret") + '"')

            } else {
                throw new GradleException(
                        "You need to have projectRoot/config/config.properties file with "
                                + "vpsApiUrl,"
                                + " to build project"
                )
            }
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //support
    String supportLibraryVersion = '28.0.0'
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:customtabs:$supportLibraryVersion"
    implementation "com.android.support:animated-vector-drawable:$supportLibraryVersion"
    implementation "com.android.support:support-media-compat:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"

    //multiDex
    implementation 'com.android.support:multidex:1.0.3'
    //CardView
    implementation 'com.android.support:cardview-v7:28.0.0'
    // logs
    implementation 'com.jakewharton.timber:timber:4.7.1'
    //DI
    implementation 'com.github.stephanenicolas.toothpick:toothpick-runtime:1.1.3'
    implementation 'com.github.stephanenicolas.toothpick:smoothie:1.1.3'
    annotationProcessor 'com.github.stephanenicolas.toothpick:toothpick-compiler:1.1.3'
    //rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.5'
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.2.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.2.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-support-v4:2.2.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-design:2.2.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:2.2.0'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.0'
    //glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    //MVP
    implementation 'com.arello-mobile:moxy:1.5.5'
    annotationProcessor 'com.arello-mobile:moxy-compiler:1.5.5'
    implementation 'com.arello-mobile:moxy-app-compat:1.5.5'
    //okhttp + retrofit
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    //room DB
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
    implementation 'android.arch.persistence.room:rxjava2:1.1.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //Navigation
    implementation 'ru.terrakok.cicerone:cicerone:3.0.0'
    //views
    implementation 'com.google.android:flexbox:1.0.0'
    implementation 'jp.wasabeef:blurry:2.1.1'
    //flags
    implementation 'com.github.KaKaVip:Android-Flag-Kit:v0.1'
    //google
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.google.firebase:firebase-core:16.0.7'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'
    implementation "com.google.firebase:firebase-crash:16.2.1"
    //vk
    implementation 'com.vk:androidsdk:1.6.9'
    //facebook
    implementation 'com.facebook.android:facebook-login:[4,5)'
}
repositories {
    maven { url 'https://jitpack.io' }
    mavenCentral()
}
